;; init-markdown.el --- Initialize markdown configurations.	-*- lexical-binding: t -*-

;; Copyright (C) 2018 Vincent Zhang

;; Author: Vincent Zhang <seagle0128@gmail.com>
;; URL: https://github.com/seagle0128/.emacs.d

;; This file is not part of GNU Emacs.
;;
;; This program is free software; you can redistribute it and/or
;; modify it under the terms of the GNU General Public License as
;; published by the Free Software Foundation; either version 2, or
;; (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;; General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with this program; see the file COPYING.  If not, write to
;; the Free Software Foundation, Inc., 51 Franklin Street, Fifth
;; Floor, Boston, MA 02110-1301, USA.
;;

;;; Commentary:
;;
;; Markdown configurations.
;;

;;; Code:

(use-package markdown-mode
  :ensure-system-package
  ((github-markup . "sudo gem install github-markup")
   (commonmarker . "sudo gem install commonmarker")
   (grip . "pip install grip")
   (markdownlint . "sudo npm i -g markdownlint-cli"))
  :defines (flycheck-markdown-markdownlint-cli-config md-lint md-file md-lint-dir)
  :preface
  (defun markdown-preview-grip ()
    "Render and preview with `grip'."
    (interactive)
    (let ((port "6419"))
      (start-process "grip" "*gfm-to-html*" "grip" (buffer-file-name) port)
      (browse-url (format "http://localhost:%s/%s.%s"
                          port
                          (file-name-base)
                          (file-name-extension
                           (buffer-file-name))))))

  (defun set-flycheck-markdownlint ()
    "Set the `mardkownlint' config file for the current buffer."
    (when-let* ((md-lint ".markdownlint.json")
                (md-file buffer-file-name)
                (md-lint-dir (locate-dominating-file md-file md-lint)))
      (setq-local flycheck-markdown-markdownlint-cli-config (concat md-lint-dir md-lint))))
  :bind (:map markdown-mode-command-map
              ("g" .  markdown-preview-grip))
  :hook ((markdown-mode . flyspell-mode)
         (markdown-mode . set-flycheck-markdownlint))
  :mode (("README\\.md\\'" . gfm-mode))
  :config
  (setq markdown-command "github-markup")
  (setq markdown-command-needs-filename t)

  ;; Preview
  (setq markdown-content-type "application/xhtml+xml")
  (setq markdown-css-paths '("https://cdn.jsdelivr.net/npm/github-markdown-css/github-markdown.min.css"
                             "http://cdn.jsdelivr.net/gh/highlightjs/cdn-release/build/styles/github.min.css"))
  (setq markdown-xhtml-header-content "
<meta name='viewport' content='width=device-width, initial-scale=1, shrink-to-fit=no'>
<style>
body {
  box-sizing: border-box;
  max-width: 740px;
  width: 100%;
  margin: 40px auto;
  padding: 0 10px;
}
</style>
<script src='http://cdn.jsdelivr.net/gh/highlightjs/cdn-release/build/highlight.min.js'></script>
<script>
document.addEventListener('DOMContentLoaded', () => {
  document.body.classList.add('markdown-body');
  document.querySelectorAll('pre[lang] > code').forEach((code) => {
    code.classList.add(code.parentElement.lang);
    hljs.highlightBlock(code);
  });
});
</script>
"))

(provide 'init-markdown)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; init-markdown.el ends here
